
	LEDW=6302	/ 12 LED OUTPUT, PUMPKIN
	LROW=6311	/ 12 LED OUTPUT, RAINBOW

	CLEI=6131       / ENABLE INTERRUPTS FROM M882
	CLDI=6132       / CLEAR INTERRUPT
	CLSK=6133       / SKIP ON CLOCK FLAG AND CLEAR FLAG

	ADSF=6531	/ SKIP IF ADC VALUE AVAILABLE 
	ADCV=6532	/ START ADC MEASUREMENT
	ADRB=6534	/ READ ADC VALUE

	ADCC=6541	/ CLEAR MUX CHANNEL
	ADSC=6542	/ SET MUX CHANNEL
	ADIC=6544	/ INC MUX CHANNEL

	*0
IRET,	0
	JMP I 2	
	INTSER

	*30
CLOCK,	0



	*200
	KCF
	ION		/ ENABLE INTERRUPTS
	CLEI
START,	CLA CLL    
	TAD MSIXTY 
	TAD CLOCK
	SNL
	JMP CHKSR
BLINK,	CLA CLL 
	DCA CLOCK
	TAD CPATT  
	CMA
	DCA CPATT
	TAD CPATT 
	LEDW  

CHKSR,	CLA CLL
	TAD CHANN
	CIA
	TAD SR
	SNA CLA 
	JMP PADC
	TAD SR
	ADSC 
	DCA CHANN
PADC,	ADCV
WTADC,	NOP		/ NOPS JUST TO PREVENT SIMH'S 
	NOP		/ INSTR PER SEC COUNT FROM 
	NOP		/ FALLING TOO DEEP             
	NOP
	NOP	
	ADSF 
	JMP WTADC
	ADRB
	LROW			


DLOOP,	ISZ DELAY	/ IN A REAL WORLD APPLICATION, 
	JMP DLOOP	/ DO SOMETHING USEFUL HERE 
	JMP START       
	

	*300            / CLOCK INTERRRUPT, JUST UPDATES CLOCK 
			/ AND SR 60 TIMES A SEC
INTSER,	DCA INTACC	/ SAVE AC
	RAR 
	DCA INTLNK	/ ANSD LINK 
	CLSK 
	JMP INTNC	/ INTERRUPT ON SOMETH ELSE, NOT CLOCK
	ISZ CLOCK	
	NOP		/ IGNORE OVERFLOW
	LAS		/ LOAD 2 LEAST SIG BITS OF SR
	AND THREE	
	DCA SR		/ STORE IN VAR
	JMP INTRET
INTNC,	KCC		/ CLEAR KEYBOARD AS LIKELY SOURCE OF INT 
INTRET,	CLA CLL     
	TAD INTLNK	/ RESTORE AC AND LINK
	RAL
	TAD INTACC
	ION
	JMP I 0	


	*360
INTACC,	0		 
INTLNK,	0
TMP,	0
CPATT,	4001		/ PUMPKIN PATTERN, EYES ON, REST OFF
MSIXTY,	7704
CHANN,	0
SR,	0	
THREE,	3
DELAY,	0
$

